//Tarun Arumugam 530001455

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    Not(in=instruction[15],out=c);
	Mux16(a=outtM,b=instruction,sel=c,out=d);
    Or(a=c,b=instruction[5],out=e);
	ARegister(in=d,load=e,out=A,out[0..14]=addressM);
	And(a=instruction[15],b=instruction[12],out=AorM);
	Mux16(a=A,b=inM,sel=AorM,out=AM);
	ALU(x=D,y=AM,zx=instruction[11],nx=instruction[10],zy=instruction[9],ny=instruction[8],f=instruction[7],no=instruction[6],out=outtM,out=outM,zr=zr,ng=ng);
	And(a=instruction[15],b=instruction[4],out=f);
	DRegister(in=outtM,load=f,out=D);
	And(a=instruction[15],b=instruction[3],out=writeM);
	Not(in=ng,out=g);
	Not(in=zr,out=h);
	And(a=instruction[15],b=instruction[0],out=i);
	And(a=g,b=h,out=j);
	And(a=i,b=j,out=load1);
	And(a=instruction[15],b=instruction[1],out=jeq);
	And(a=jeq,b=zr,out=load2);
	And(a=instruction[15],b=instruction[2],out=jlt);
	And(a=jlt,b=ng,out=load3);
	Or(a=load1,b=load2,out=ldt);
	Or(a=load3,b=ldt,out=loadf);
	PC(in=A,load=loadf,inc=true,reset=reset,out[0..14]=pc);
}